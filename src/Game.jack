class Game{
    field GameFilled gameFilled;
    field int bestScore;
    field String gameOver, pressAnyButton, forPlayAgain, youLose, youWin, bestScoreStr, youCloseGameBye;

    constructor Game new(GameFilled _gameFilled)
    {
        let gameFilled = _gameFilled;
        let bestScore = 0;
        let gameOver = "Game Over!";
        let pressAnyButton = "Press any button";
        let forPlayAgain = "for play again";
        let youLose = "You lose";
        let youWin = "You isWin";
        let bestScoreStr = "Best score:";
        let youCloseGameBye = "You close game, bye!";
        do setBestScore();
        return this;
    }

    /* Базовый метод работы игры 
        1 - сменить игровой режим на 3 на 3
        2 - сменить игровой режим на 4 на 4
        3 - сменить игровой режим на 5 на 5
        4 - выйти из игры
        W or Up - вверх
        S or Down - вниз
        A or Left - влево
        D or Right - вправо
        */
	method void run() 
    {
		var char key;
        var int size, score;
        var boolean exit, isWin;

        let exit = false;
        let isWin = false;
		while (~exit)
        {
			while (key = 0) 
            {
				let key = Keyboard.keyPressed(); 
                let size = gameFilled.getSize();
                let isWin = gameFilled.getWin();

				if ((key = 49) & (~(size = 3))) // press 1
                {
                    do updateGameFilled(3);
				} 
                
				if ((key = 50) & (~(size = 4))) // press 2
                { 
                    do updateGameFilled(4);
				}

				if ((key = 51) & (~(size = 5))) // press 3
                { 
                   do updateGameFilled(5);
				}

                if ((key = 52) & (~(size = 2)))  // press 4
                { 
                    do drawEndScreen();
                    let exit = true;
				}  

                if ((key = 68) | (key = 132))
                {
                    do gameFilled.moveRight(); // press D or Right
                    do setBestScore();
                }

                if ((key = 65) | (key = 130))
                {
                    do gameFilled.moveLeft(); // press A or Left
                    do setBestScore();   
                }

                if ((key = 87) | (key = 131))
                {
                    do gameFilled.moveUp(); // press W or Up
                    do setBestScore();   
                }

                if ((key = 83) | (key = 133))
                {
                    do gameFilled.moveDown(); // press S  or Down
                    do setBestScore();
                }
				
                // Проверка конца
                let isWin = gameFilled.getWin();
                if ((gameFilled.isGameOver()) | isWin) 
                {
                    do drawGameOver(isWin);
                    do gameFilled.dispose();
                    do Sys.wait(2000);

                    let key = Keyboard.keyPressed(); 
                    while ((key = 0))
                    {
					    let key = Keyboard.keyPressed(); 
				    }
                    do Screen.clearScreen();
                    let gameFilled = GameFilled.new(size);
                }

				while ((~(key = 0)) & (~(exit)))
                {
					let key = Keyboard.keyPressed(); 
				}
			} 
            
        }
        return;
    }

    method void drawEndScreen()
    {
        do Screen.clearScreen();
        do Output.moveCursor(8,20);
        do DrawerNum.draw2048T(200,100);
        do Output.printString(youCloseGameBye);
        do gameFilled.dispose();
        return;
    }

    method void setBestScore()
    {
        var int score;

        let score = gameFilled.getScore();
        if (score > bestScore)
        {
            let bestScore = score;
            do Output.moveCursor(1,48);
            do Output.printString(bestScoreStr);
            do Output.printInt(bestScore);                      
        }
        else
        {
            do Output.moveCursor(1,48);
            do Output.printString(bestScoreStr);
            do Output.printInt(bestScore);    
        }
        
        return;
    }

    method void drawGameOver(boolean isWin)
    {
        if (isWin)
        {
            do Output.moveCursor(10,28);
            do Output.printString(youWin);
        }

        else
        {
            do Output.moveCursor(10,28);
            do Output.printString(youLose);
        }
        do Output.moveCursor(11,27);
        do Output.printString(gameOver);
        do Output.moveCursor(12,25);
        do Output.printString(pressAnyButton);
        do Output.moveCursor(13,25);
        do Output.printString(forPlayAgain);
        return;
    }

    method void updateGameFilled(int size)
    {
        do Screen.clearScreen();
        do gameFilled.dispose();
        let gameFilled = GameFilled.new(size); // press 1
        do setBestScore();
        return;
    }
}